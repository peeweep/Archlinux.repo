# $Id: PKGBUILD 184754 2016-08-01 15:30:30Z felixonmars $
# Maintainer: RemiliaForever <remilia AT koumakan DOT cc>
# Contributor: Felix Yan <felixonmars@gmail.com>
# Contributor: Christoph Drexler <chrdr at gmx dot at>
# Contributor: Jelle van der Waa <jellevdwaa@gmail.com>

# GTK_VERSION 2/3
GTK_VERSION=3
# JAVA_VERSION 8/10
JAVA_VERSION=10
# JAVA_PATH
#JAVA_PATH='/usr/lib/jvm/default-runtime/bin/java'

pkgname=xmind
pkgver=3.7.8+8update8
_filename=$pkgname-8-update8-linux
pkgrel=1
pkgdesc="Brainstorming and Mind Mapping Software"
arch=('i686' 'x86_64')
url="http://www.xmind.net"
license=('EPL' 'LGPL')
depends=('java-runtime>=8')
optdepends=('gtk2: gtk2 or gtk3 must install one'
            'gtk3: gtk2 or gtk3 must install one'
            'lame: needed for the feature audio notes')
install=xmind.install
source=("http://www.xmind.net/xmind/downloads/${_filename}.zip"
'XMind'
'xmind.desktop'
'xmind.xml'
'xmind.png'
'xmind_file.png')
sha512sums=('77c5c05801f3ad3c0bf5550fa20c406f64f3f5fa31321a53786ac1939053f5c4f0d0fb8ab1af0a9b574e3950342325b9c32cf2e9a11bf00a1d74d2be1df75768'
            'b2fa644ab4dad7ee75c92b97dbb6089b269ca9303e06c6bdc68561ac88e82c25f9ea302095532a72e2151c8f7d20a62cba5a0378510b9211ecc2f4a539dc6f40'
            'c3d1eacefd1412357d963f0f80ecaeffb0c8a234aa19cc94866922bf3387977214c42435c798f33e90c5e7ed868e440f0ccd2b1f945c20d8c67649c7dcb991f8'
            'eb8f04246c43aae5430a5f6549dfd6cb5aed869b2f2ae382a2a1e71e1ff426d08f9078dd446cea0078e71a3849f953f277437b94daf2b731a8e124d109b7aea6'
            'a12b63ac83e946477341157178486dce01cc6d59192c1505bc78cdb62313e3f54bb3bf020ede643aac21707b3e05d2674131f7144e058e61ddf2ef9009d93aab'
            '75a6cd23ab582a5e47e2feed69b02cf05422b0f5790e2b89c9bb805ecd7a75451341c2453f18f6d7a954041952aa2f12242b45c4ac5d465fc6f744464841e161')

package() {
    mkdir -p ${pkgdir}/usr/share/${pkgname}
    cp -r ${srcdir}/configuration   ${pkgdir}/usr/share/${pkgname}/
    cp -r ${srcdir}/features        ${pkgdir}/usr/share/${pkgname}/
    cp -r ${srcdir}/plugins         ${pkgdir}/usr/share/${pkgname}/
    cp ${srcdir}/*.xml              ${pkgdir}/usr/share/${pkgname}/
    mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
    cp ${srcdir}/{epl-v10,lgpl-3.0}.html    ${pkgdir}/usr/share/licenses/${pkgname}/
    cp ${srcdir}/xpla.txt                   ${pkgdir}/usr/share/licenses/${pkgname}/
    if [[ "$CARCH" == "i686" ]]; then
        cp -r ${srcdir}/XMind_i386  ${pkgdir}/usr/share/${pkgname}/XMind
    else
        cp -r ${srcdir}/XMind_amd64 ${pkgdir}/usr/share/${pkgname}/XMind
    fi
    mkdir -p ${pkgdir}/usr/share/fonts/${pkgname}
    cp -r ${srcdir}/fonts           ${pkgdir}/usr/share/fonts/${pkgname}/
    mkdir -p ${pkgdir}/usr/share/applications
    cp ${srcdir}/xmind.desktop      ${pkgdir}/usr/share/applications/
    mkdir -p ${pkgdir}/usr/share/mime/packages
    cp ${srcdir}/xmind.xml          ${pkgdir}/usr/share/mime/packages/
    mkdir -p ${pkgdir}/usr/share/pixmaps
    cp ${srcdir}/*.png              ${pkgdir}/usr/share/pixmaps/
    # fix configuration
    sed -i "s|^./configuration$|@user.home/.xmind/configuration|" ${pkgdir}/usr/share/${pkgname}/XMind/XMind.ini
    sed -i "s|^../workspace$|@user.home/.xmind/workspace|" ${pkgdir}/usr/share/${pkgname}/XMind/XMind.ini
    if [[ "$GTK_VERSION" != "2" ]]; then
        sed -i "s|^2$|3|" ${pkgdir}/usr/share/${pkgname}/XMind/XMind.ini
    fi
    if [[ "$JAVA_VERSION" != "8" ]]; then
        echo "--add-modules=java.se.ee" >> ${pkgdir}/usr/share/${pkgname}/XMind/XMind.ini
    fi
    if [[ "$JAVA_PATH" != "" ]]; then
        sed -i "s|^-vmargs$|-vm\n${JAVA_PATH}\n-vmargs|" ${pkgdir}/usr/share/${pkgname}/XMind/XMind.ini
    fi
    mkdir -p ${pkgdir}/usr/bin
    cp ${srcdir}/XMind              ${pkgdir}/usr/bin/
}
